@"if OBJECT_ID('tempdb..#sizes') IS NOT NULL
	DROP TABLE #sizes
if OBJECT_ID('tempdb..#available') IS NOT NULL
	DROP TABLE #available

create table #sizes(
	database_id int
	,IndexUsage bigint
	,DataSpaceUsage bigint
)
create table #available (
	database_id int,
	type int,
	SpaceAvailable bigint
)
exec sp_MSforeachdb N'USE [?] INSERT INTO #sizes
	select  db_id() as database_id,(sum(used_pages)-sum(
				CASE
					-- XML-Index and FT-Index and semantic index internal tables are not considered "data", but is part of "index_size"
					When it.internal_type IN (202,204,207,211,212,213,214,215,216,221,222,236) Then 0
					When a.type <> 1 and p.index_id < 2 Then a.used_pages
					When p.index_id < 2 Then a.data_pages
					Else 0
				END
			)) * 8192 / 1024 AS IndexUsage
			,sum(
				CASE
					-- XML-Index and FT-Index and semantic index internal tables are not considered "data", but is part of "index_size"
					When it.internal_type IN (202,204,207,211,212,213,214,215,216,221,222,236) Then 0
					When a.type <> 1 and p.index_id < 2 Then a.used_pages
					When p.index_id < 2 Then a.data_pages
					Else 0
				END
			) * 8 AS DataSpaceUsage
	from sys.partitions p join sys.allocation_units a on p.partition_id = a.container_id
		left join sys.internal_tables it on p.object_id = it.object_id'

exec sp_MSforeachdb N'USE [?] 
	insert into #available
	select db_id() as database_id
	,type
	,sum((size * 8) -(FILEPROPERTY(name,''SpaceUsed''))*8) as SpaceAvailable
from sys.database_files
WHERE type = 0
group by type'

select sdb.name
	,sdb.create_date as [CreationDate]
	,sdb.compatibility_level AS [Compatibility]
	,sdb.collation_name as [Collation]
	,sdb.is_read_only as [ReadOnly]
	,sdb.is_auto_close_on AS [AutoClose]
	,sdb.is_auto_shrink_on AS [AutoShrink]
	,sdb.recovery_model_desc AS [RecoveryModel]
	,sdb.page_verify_option_desc AS [PageVerify]
	,mf.Size
	,s.DataSpaceUsage
	,s.IndexUsage
	,a.SpaceAvailable
	,drs.database_guid
from sys.databases sdb
inner join
(
	select database_id,(sum(size) * 8)/1024 as Size
	from sys.master_Files 
	group by database_id
) mf on sdb.database_id = mf.database_id 
INNER JOIN #sizes s on sdb.database_id = s.database_id
INNER JOIN #available a on sdb.database_id = a.database_id
INNER JOIN sys.database_recovery_status drs ON drs.database_id = sdb.database_id"